% PT Scanner.ssl   Ver. 1.04
%
% Date:  30 Jun 1980
%	(revised 30 Dec 2016)
%
% Author:  Alan Rosselet
%
% Abstract
%     The scanner breaks a PT source program up into syntax tokens.
%   It removes token separators - blanks, tabs, comments.  The
%   scanner detects these lexical errors:  unclosed comments,
%   unclosed literals, literals that cross a line boundary,
%   illegal characters and integers followed by identifiers with no
%   separator between them.

Input :
	lLetter 
	firstCharClass = lLetter 
	lDigit
	lPlus		'+'
	lMinus		'-'
	lStar		'*'
	lColon		':'
	lEquals		'='
	lDot		'.'
	lComma		','
	lSemicolon	';'
	lQuote
	lLeftAngle	'<'
	lRightAngle	'>'
	lLeftParen	'('
	lRightParen	')'
	lLeftBracket	'['
	lRightBracket	']'
	lPercent '%'
	lHash    '#'
	lExclamation '!'
	lQuestion '?'
    	lDollar '$'
	lBlank
	lIllegal
	lNewLine
	lTab
	lNewPage 
	lEndFile
	lastCharClass = lEndFile;

Output :
	% Keyword tokens -
	%   Tokens pDiv through pRepeat are keywords which will be separated
	%   from user defined identifiers by the Screener.  This Scanner
	%   treats them all as identifiers.
	pDiv 
	firstKeywordToken = pDiv
	pMod
	pOr
	pAnd
	pThen
	pElse
	pOf
	pEnd
	pDo
	pArray
	pFile
	pPacked
	pVar
	pType
	pIf
	pCase
	pWhile
	pUsing
	pVal
	pDef
	pUnless
	pElsif
	pBreak
	pWhen
	pModule
	lastKeywordToken = pModule

	% Scanner tokens -
	%   These are the tokens actually recognized by the Scanner itself.

	% Compound tokens - 
	%   These will be sent to the parser with a value or index.
	pIdentifier 
	firstCompoundInputToken = pIdentifier
	pInteger
	pLiteral
	lastCompoundInputToken = pLiteral

	% Non-compound tokens - 
	%   These tokens simply represent themselves.
	pNewLine
	pEndFile
	pPlus
	pMinus
	pStar
	pAssign
	pDot
	pComma
	pSemicolon
	pColon
	pEquals
	pNotEqual
	pLess
	pLessEqual
	pGreaterEqual
	pGreater
	pLeftParen
	pRightParen
	pLeftBracket
	pRightBracket
	pDotDot
    pDoubleEquals
    pNot
    pQuestion
    pDollar
    pHash
	lastSyntaxToken = pHash;

Error :
	eIllegalChar = 5
	firstScanError = eIllegalChar
	eLiteralLine
	eLiteralEOF
	eCommentEOF
	eNoSeparator
	lastScanError = eNoSeparator;

mechanism Buffer :
    % Buffer saves the characters associated with the
    % compound tokens identifier, integer and literal.

    oBufferSave;
	% Save the character form of the last accepted lexical token.




rules

% Note that the order of choice alternatives
% may have a significant effect on performance
% under some implementations of S/SL.
Scan :
	{[
	    | lBlank :
	    | lLetter:
		oBufferSave
		@Identifier
	    | lNewLine:
		.pNewLine
	    | lTab:
	    | ';':
		.pSemicolon
	    | '(':
	    .pLeftParen
	    | ')':
		.pRightParen
	    | '[':
		.pLeftBracket
	    | ']':
		.pRightBracket
	    | ',':
		.pComma
	    | ':':
		.pColon
	    | lDigit:
		oBufferSave
		@Integer
		[
		    | lLetter:
			#eNoSeparator
			oBufferSave
			@Identifier
		    | *:
		]
	    | '=':
        [
            | '=':
            .pDoubleEquals
            | *:
		    .pAssign
        ]
	    | '+':
		.pPlus
	    | lQuote:
		@Literal
	    | '-':
		.pMinus
	    | '.':
		[
		    | '.':
			.pDotDot
		    | *:
			.pDot
		]
	    | '<':
		[
		    | '=':
			.pLessEqual
		    | *:
			.pLess
		]
        | '!':
        [
            | '=' :
            .pNotEqual
            | * :
            .pNot
        ]
	    | '>':
		[
		    | '=':
			.pGreaterEqual
		    | *:
			.pGreater
		]
        | '%':
        @Comment
	    | '*':
		.pStar
        | '?':
        .pQuestion
        | '$':
        .pDollar
        | '#':
        .pHash
	    | lNewPage:
	    | lEndFile:
		.pEndFile
	    | *:
		?
		#eIllegalChar
	]};

Identifier :
	% Assemble an identifier token

	{[
	    | lLetter, lDigit:
		oBufferSave
	    | *:
		.pIdentifier
		>
	]};

Integer :
	% Assemble an integer token

	{[
	    | lDigit:
		oBufferSave
	    | *:
		.pInteger
		>
	]};

Literal :
	% Assemble a literal string token

	{[
	    | lQuote:
        .pLiteral
        >
	    | lNewLine:		% literal must not cross a line boundary
		#eLiteralLine
		.pNewLine
		>
	    | lEndFile:		% literal must be closed before EOF
		#eLiteralEOF
		.pEndFile
		>
	    | *:	% literal may contain any other character
		?
		oBufferSave
	]};

Comment :
	% Discard the contents of a comment

	{[
	    | lNewLine:     % comments are closed by a newline
		.pNewLine
		>
	    | lEndFile:     % comments need not be closed before EOF
		.pEndFile
		>
	    | *:	% comment may contain any other character
		?
	]};

end
